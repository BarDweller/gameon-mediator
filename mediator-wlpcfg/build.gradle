apply plugin: 'eclipse'

buildscript {
	repositories {
	    mavenCentral()
	}
	
	dependencies {
		classpath 'com.bmuschko:gradle-docker-plugin:2.6'
	}
}


task copyWAR(type: Copy) {
    from '../player-app/build/libs/player-app-1.0.war'
    into 'servers/gameon-player/apps/'
    rename("player-app-1.0.war", "player-app.war")
}

task copyMongo(type: Copy) {
    from '../player-app/build/libs/mongo-java-driver-2.12.3.jar'
    into 'servers/gameon-player/lib/'
}

apply plugin: 'com.bmuschko.docker-remote-api'

import com.bmuschko.gradle.docker.tasks.container.*
import com.bmuschko.gradle.docker.tasks.image.*

docker {
    url = System.getenv('GRADLE_DOCKER_HOST')
    certPath = new File(System.getenv("WORKSPACE") + "/dockercfg")
}

task buildDockerImage(type: DockerBuildImage, dependsOn: ['copyWAR','copyMongo']) {
	inputDir = file('.')
    tag = 'gameon-player'
    quiet = false
}

task stopCurrentContainer(type: DockerStopContainer) {
    targetContainerId { "gameon-player" }
    timeout 0
}

task removeCurrentContainer(type: DockerRemoveContainer) {
	targetContainerId { "gameon-player" }
}

task createNewContainer(type: DockerCreateContainer) {
	targetImageId { "gameon-player" }
	containerName = "gameon-player"
	portBindings = ['9443:9443']
	env = ["LICENSE=accept", 
			"CONCIERGE_URL=" + System.getenv('CONCIERGE_URL'),
			"CONCIERGE_KEY=" + System.getenv('CONCIERGE_KEY'),
			"MONGO_HOST=" + System.getenv('MONGO_HOST'),
			"MONGO_PORT=" + System.getenv('MONGO_PORT'),
			"LOGGING_DOCKER_HOST=" + System.getenv("LOGGING_DOCKER_HOST"),
			"TWITTER_CONSUMER_KEY=" + System.getenv('TWITTER_CONSUMER_KEY'),
			"TWITTER_CONSUMER_SECRET=" + System.getenv('TWITTER_CONSUMER_SECRET'),
			"FACEBOOK_APP_ID=" + System.getenv('FACEBOOK_APP_ID'),
			"FACEBOOK_APP_SECRET=" + System.getenv('FACEBOOK_APP_SECRET'),
			"SUCCESS_CALLBACK=" + System.getenv('SUCCESS_CALLBACK'),
			"PLAYER_URL=" + System.getenv('PLAYER_URL')
		]
}

task startNewContainer(type: DockerStartContainer) {
	dependsOn createNewContainer
	targetContainerId { "gameon-player" }
}

