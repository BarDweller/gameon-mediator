apply plugin: 'eclipse'

buildscript {
	repositories {
	    mavenCentral()
	}

	dependencies {
		classpath 'com.bmuschko:gradle-docker-plugin:2.6'
	}
}

def dockerHost = "https://localhost:2376"
if (null != System.getenv("DOCKER_HOST"))
{
	dockerHost = System.getenv('DOCKER_HOST')

	if (dockerHost.indexOf('tcp') == 0)
	{
		dockerHost = dockerHost.replace('tcp','https')
	}
}

def dockerCertPath = ""
if (null != System.getenv('DOCKER_CERT_PATH')){
	dockerCertPath = System.getenv('DOCKER_CERT_PATH')
}

def localEnvironment = ["LICENSE=accept",
			"CONCIERGE_URL=" + System.getenv('CONCIERGE_URL'),
			"CONCIERGE_KEY=" + System.getenv('CONCIERGE_KEY'),
			"PLAYER_URL=" + System.getenv('PLAYER_URL')
		]

if (null != System.getenv("SSL_CERT"))
{
	localEnvironment << "SSL_CERT=" + System.getenv("SSL_CERT")
}

def etcdEnvironment = ["LICENSE=accept", "ETCDCTL_ENDPOINT=http://etcd:4001"]


task copyWAR(type: Copy) {
    from '../mediator-app/build/libs/mediator-app-1.0.war'
    into 'servers/gameon-mediator/apps/'
    rename("mediator-app-1.0.war", "mediator-app.war")
}

task build(dependsOn: ['copyWAR']){
}

apply plugin: 'com.bmuschko.docker-remote-api'

import com.bmuschko.gradle.docker.tasks.container.*
import com.bmuschko.gradle.docker.tasks.image.*

docker {
	  url = dockerHost
    certPath = new File(dockerCertPath)
}

task buildDockerImage(type: DockerBuildImage, dependsOn: ['copyWAR']) {
	inputDir = file('.')
    tag = 'gameon-mediator'
    quiet = false
}

task stopCurrentContainer(type: DockerStopContainer) {
    targetContainerId { "gameon-mediator" }
    timeout 0
}

task removeCurrentContainer(type: DockerRemoveContainer) {
	targetContainerId { "gameon-mediator" }
}

task createNewContainer(type: DockerCreateContainer) {
	targetImageId { "gameon-mediator" }
	containerName = "gameon-mediator"
	env = localEnvironment
	portBindings = ['9086:9080','9446:9443']
}

task startNewContainer(type: DockerStartContainer) {
	dependsOn createNewContainer
	targetContainerId { "gameon-mediator" }
}

task createNewEtcdContainer(type: DockerCreateContainer) {
	targetImageId { "gameon-mediator" }
	containerName = "gameon-mediator"
	portBindings = ['9086:9080','9446:9443']
    env = etcdEnvironment
    links = ['etcd:etcd']
}

task startNewEtcdContainer(type: DockerStartContainer) {
	dependsOn createNewEtcdContainer
	targetContainerId { "gameon-mediator" }
}
